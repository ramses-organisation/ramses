subroutine adaptive_loop
    use amr_commons
    use hydro_commons
    use pm_commons
    use poisson_commons
    use cooling_module
    ! ~~~~~~~~~ begin ~~~~~~~~~
    use mond_parameters
    use mond_commons
    ! ~~~~~~~~~~ end ~~~~~~~~~~
#ifdef RT
    use rt_hydro_commons
#endif
    implicit none
#ifndef WITHOUTMPI
    include 'mpif.h'
#endif
    integer :: ilevel, idim, ivar, info
    real(kind=8) :: tt1, tt2
    real(kind=4) :: real_mem, real_mem_tot

#ifndef WITHOUTMPI
    tt1 = MPI_WTIME()
#endif

    ! ~~~~~~~~~ begin ~~~~~~~~~
    ! Associate the rho_tot pointer, if this is not the case (at the beginning)
    if (.not. associated(rho_tot)) then
        rho_tot => rho_newton_tot
    end if
    ! ~~~~~~~~~~ end ~~~~~~~~~~

    call init_amr                      ! Initialize AMR variables
    call init_time                     ! Initialize time variables
    if (hydro) call init_hydro           ! Initialize hydro variables
#ifdef RT
    if (rt .or. neq_chem) &
        call rt_init_hydro            ! Initialize radiation variables
#endif
    if (poisson) call init_poisson       ! Initialize poisson variables
    ! ~~~~~~~~~ begin ~~~~~~~~~
    if (mond)   call init_mond          ! Initialize variables
    ! ~~~~~~~~~~ end ~~~~~~~~~~
#ifdef ATON
    if (aton) call init_radiation        ! Initialize radiation variables
#endif
    if (nrestart == 0) call init_refine    ! Build initial AMR grid
    if (cooling .and..not. neq_chem) &
        call set_table(dble(aexp))    ! Initialize cooling look up table
    if (pic) call init_part              ! Initialize particle variables
    if (pic) call init_tree              ! Initialize particle tree
    if (nrestart == 0) call init_refine_2  ! Build initial AMR grid again

#ifndef WITHOUTMPI
    tt2 = MPI_WTIME()
    if (myid == 1) write(*, *) 'Time elapsed since startup:', tt2 - tt1
#endif

    if (myid == 1) then
        write(*, *) 'Initial mesh structure'
        do ilevel = 1, nlevelmax
            if (numbtot(1, ilevel) > 0) write(*, 999) ilevel, numbtot(1:4, ilevel)
        end do
    end if

    nstep_coarse_old = nstep_coarse

    if (myid == 1) write(*, *) 'Starting time integration'

    do ! Main time loop

#ifndef WITHOUTMPI
        tt1 = MPI_WTIME()
#endif

        if (verbose) write(*, *) 'Entering amr_step_coarse'

        epot_tot = 0.0D0  ! Reset total potential energy
        ekin_tot = 0.0D0  ! Reset total kinetic energy
        mass_tot = 0.0D0  ! Reset total mass
        eint_tot = 0.0D0  ! Reset total internal energy
#ifdef SOLVERmhd
        emag_tot = 0.0D0  ! Reset total magnetic energy
#endif

        ! Make new refinements
        if (levelmin < nlevelmax .and..not. static) then
            call refine_coarse
            do ilevel = 1, levelmin
                call build_comm(ilevel)
                call make_virtual_fine_int(cpu_map(1), ilevel)
                if (hydro) then
#ifdef SOLVERmhd
                    do ivar = 1, nvar + 3
#else
                        do ivar = 1, nvar
#endif
                            call make_virtual_fine_dp(uold(1, ivar), ilevel)
#ifdef SOLVERmhd
                        end do
#else
                    end do
#endif
                    if (simple_boundary) call make_boundary_hydro(ilevel)
                end if
#ifdef RT
                if (rt) then
                    do ivar = 1, nrtvar
                        call make_virtual_fine_dp(rtuold(1, ivar), ilevel)
                    end do
                    if (simple_boundary) call rt_make_boundary_hydro(ilevel)
                end if
#endif
                if (poisson) then
                    ! ~~~~~~~~~ begin ~~~~~~~~~
                    if (mond) then
                        ! Newton
                        call make_virtual_fine_dp(phi_newton(1), ilevel)
                        do idim = 1, ndim
                            call make_virtual_fine_dp(f_newton(1, idim), ilevel)
                        end do
                        ! MOND
                        call make_virtual_fine_dp(phi_mond(1), ilevel)
                        do idim = 1, ndim
                            call make_virtual_fine_dp(f_mond(1, idim), ilevel)
                        end do
                    else
                        call make_virtual_fine_dp(phi(1), ilevel)
                        do idim = 1, ndim
                            call make_virtual_fine_dp(f(1, idim), ilevel)
                        end do
                    end if
                    ! ~~~~~~~~~~ end ~~~~~~~~~~
                end if
                if (ilevel < levelmin) call refine_fine(ilevel)
            end do
        end if

        ! Call base level
        call amr_step(levelmin, 1)

        if (levelmin < nlevelmax .and..not. static) then
            do ilevel = levelmin - 1, 1, - 1
                ! Hydro book-keeping
                if (hydro) then
                    call upload_fine(ilevel)
#ifdef SOLVERmhd
                    do ivar = 1, nvar + 3
#else
                        do ivar = 1, nvar
#endif
                            call make_virtual_fine_dp(uold(1, ivar), ilevel)
#ifdef SOLVERmhd
                        end do
#else
                    end do
#endif
                    if (simple_boundary) call make_boundary_hydro(ilevel)
                end if
#ifdef RT
                ! Radiation book-keeping
                if (rt) then
                    call rt_upload_fine(ilevel)
                    do ivar = 1, nrtvar
                        call make_virtual_fine_dp(rtuold(1, ivar), ilevel)
                    end do
                    if (simple_boundary) call rt_make_boundary_hydro(ilevel)
                end if
#endif
                ! Gravity book-keeping
                if (poisson) then
                    ! ~~~~~~~~~ begin ~~~~~~~~~
                    if (mond) then
                        ! Newton
                        call make_virtual_fine_dp(phi_newton(1), ilevel)
                        do idim = 1, ndim
                            call make_virtual_fine_dp(f_newton(1, idim), ilevel)
                        end do
                        ! MOND
                        call make_virtual_fine_dp(phi_mond(1), ilevel)
                        do idim = 1, ndim
                            call make_virtual_fine_dp(f_mond(1, idim), ilevel)
                        end do
                    else
                        call make_virtual_fine_dp(phi(1), ilevel)
                        do idim = 1, ndim
                            call make_virtual_fine_dp(f(1, idim), ilevel)
                        end do
                    end if
                    ! ~~~~~~~~~~ end ~~~~~~~~~~
                end if
            end do

            ! Build refinement map
            do ilevel = levelmin - 1, 1, - 1
                call flag_fine(ilevel, 2)
            end do
            call flag_coarse
        end if

        ! New coarse time-step
        nstep_coarse = nstep_coarse + 1

#ifndef WITHOUTMPI
        tt2 = MPI_WTIME()
        if (mod(nstep_coarse, ncontrol) == 0) then
            call getmem(real_mem)
            call MPI_ALLREDUCE(real_mem, real_mem_tot, 1, MPI_REAL, MPI_MAX, MPI_COMM_WORLD, info)
            if (myid == 1) then
                write(*, *) 'Time elapsed since last coarse step:', tt2 - tt1
                call writemem(real_mem_tot)
            end if
        end if
#endif

    end do

999 format(' Level ', I2,' has ', I10,' grids (', 3(I8,','),')')

end subroutine adaptive_loop
